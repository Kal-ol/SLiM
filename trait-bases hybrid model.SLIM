//////////////////////////////
//  UTILITY FUNCTIONS

function (void) addAllele(object<Genome> g, object<MutationType> mt, integer pos)
{
	muts = g.mutationsOfType(mt);
	if (size(muts) > 0) {
		if (sum(muts.position == pos) > 0) return;
	}
	g.addNewDrawnMutation(mt, pos);
}

function (void) setOffspringTrait(object<Individual> kid, object<Individual> mom, object<Individual> dad)
{
	z1 = mom.getValue("z");
	z2 = dad.getValue("z");
	if (isNULL(z1)) z1 = 0.0;
	if (isNULL(z2)) z2 = 0.0;

	kid.setValue("z", (z1 + z2)/2.0 + rnorm(1, 0.0, sigma_z));
	kid.setSpatialPosition(c(kid.getValue("z"), 0.0));
}

function (object<Individual>) chooseMateAssort(object<Individual> focal, object<Individual> candidates)
{
	if (size(candidates) == 0)
		return NULL;

	zf = focal.getValue("z");
	if (isNULL(zf)) zf = 0.0;

	cands = c();
	weights = c();

	for (m in candidates) {

		if (excludeSelf) {
			if (m != focal) {
				zm = m.getValue("z");
				if (isNULL(zm)) zm = 0.0;

				dz = zf - zm;
				w  = exp(-(dz*dz) / (2.0*sigma_m*sigma_m));
				if (w < epsilon_w) w = epsilon_w;

				cands   = c(cands, m);
				weights = c(weights, w);
			}
		}
		else {
			zm = m.getValue("z");
			if (isNULL(zm)) zm = 0.0;

			dz = zf - zm;
			w  = exp(-(dz*dz) / (2.0*sigma_m*sigma_m));
			if (w < epsilon_w) w = epsilon_w;

			cands   = c(cands, m);
			weights = c(weights, w);
		}
	}

	if (size(cands) == 0)
		return sample(candidates, 1);

	if (sum(weights) <= 0.0)
		return sample(cands, 1);

	return sample(cands, 1, weights=weights);
}


// Seed 
function (void) seedPrivateDMIs(object<Subpopulation> pop, object<MutationType> mt, integer nSeed)
{
	usedPos = integer(0);

	for (i in 1:nSeed) {
		ind = sample(pop.individuals, 1);

		pos = rdunif(1, 0, L-1);

		while (T) {
			ok = T;
			if (size(usedPos) > 0) {
				if (sum(usedPos == pos) > 0)
					ok = F;
			}
			if (ok)
				break;

			pos = rdunif(1, 0, L-1);
		}

		usedPos = c(usedPos, pos);

		// seed as heterozygote
		if (runif(1) < 0.5)
			addAllele(ind.genome1, mt, pos);
		else
			addAllele(ind.genome2, mt, pos);
	}
}



function (integer) copiesOfMutation(object<Individual> ind, object<Mutation> mu)
{
	c1 = ind.genome1.containsMutations(mu);
	c2 = ind.genome2.containsMutations(mu);
	return asInteger(c1) + asInteger(c2);
}



function (numeric) bdmiMultiplierOrleach(object<Individual> ind)
{
	
	M1 = unique(ind.genomes.mutationsOfType(m1));
	M2 = unique(ind.genomes.mutationsOfType(m2));

	if (size(M1) == 0) return 1.0;
	if (size(M2) == 0) return 1.0;

	w_total = 1.0;

	for (mu1 in M1) {
		n1 = copiesOfMutation(ind, mu1);
		for (mu2 in M2) {
			n2 = copiesOfMutation(ind, mu2);

			if (n1 == 2) {
				if (n2 == 2)
					w_pair = 1.0 - sb;
				else
					w_pair = 1.0 - hb * sb;
			} else {
				w_pair = 1.0 - hb * sb;
			}

			if (w_pair < 0.01) w_pair = 0.01;
			w_total = w_total * w_pair;
		}
	}

	return w_total;
}



function (void) migrateFlaggedHybrids(void)
{
	migrants = c();

	for (ind in c(p1.individuals, p2.individuals)) {
		flag = ind.getValue("migrateToP3");
		if (!isNULL(flag))
			if (flag) migrants = c(migrants, ind);
	}

	if (size(migrants) > 0) {
		p3.takeMigrants(migrants);
		for (ind in migrants)
			ind.setValue("migrateToP3", F);
	}
}


function (void) applyMacArthurCompetitionInterPop(void)
{
	
	for (pop in sim.subpopulations) {
		for (ind in pop.individuals) {
			z = ind.getValue("z");
			if (isNULL(z)) { z = 0.0; ind.setValue("z", z); }
			ind.setSpatialPosition(c(z, 0.0));
		}
	}

	
	i1.evaluate(sim.subpopulations);

	
	i2.evaluate(p1);
	i3.evaluate(p2);
	i4.evaluate(p3);

	
	for (ind in p1.individuals) {
		Dall = i1.totalOfNeighborStrengths(ind);
		Din  = i2.totalOfNeighborStrengths(ind);
		Di   = Dall - Din;
		if (Di < 0.0) Di = 0.0;

		w_comp = exp(-gamma_comp * Di);
		if (w_comp < 0.01) w_comp = 0.01;
		ind.fitnessScaling = w_comp;
	}

	for (ind in p2.individuals) {
		Dall = i1.totalOfNeighborStrengths(ind);
		Din  = i3.totalOfNeighborStrengths(ind);
		Di   = Dall - Din;
		if (Di < 0.0) Di = 0.0;

		w_comp = exp(-gamma_comp * Di);
		if (w_comp < 0.01) w_comp = 0.01;
		ind.fitnessScaling = w_comp;
	}

	for (ind in p3.individuals) {
		Dall = i1.totalOfNeighborStrengths(ind);
		Din  = i4.totalOfNeighborStrengths(ind);
		Di   = Dall - Din;
		if (Di < 0.0) Di = 0.0;

		w_comp = exp(-gamma_comp * Di);
		if (w_comp < 0.01) w_comp = 0.01;
		ind.fitnessScaling = w_comp;
	}
}



function (void) applyBDMI(void)
{
	for (pop in sim.subpopulations) {
		for (ind in pop.individuals) {
			w_bdmi = bdmiMultiplierOrleach(ind);
			ind.fitnessScaling = ind.fitnessScaling * w_bdmi;
			if (ind.fitnessScaling < 0.01) ind.fitnessScaling = 0.01;
		}
	}
}


//////////////////////////////
//  SURVIVAL

survival()
{
	// annual: adults die after reproduction
	if (individual.age > 0)
		return F;

	// per-pop soft K factor (intraspecific regulation happens here
	if (subpop == p1)
		ratio = p1.individualCount / K_p1;
	else if (subpop == p2)
		ratio = p2.individualCount / K_p2;
	else
		ratio = p3.individualCount / K_p3;

	// soft-K survival multiplier
	wK = 1.0 / (1.0 + (ratio ^ betaK));

	// combine with viability effects already in fitnessScaling
	p = individual.fitnessScaling * wK;

	if (p < 0.01) p = 0.01;
	if (p > 0.99) p = 0.99;

	return (runif(1) < p);
}


//////////////////////////////
//  INITIALIZE

initialize()
{
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(keepPedigrees=F, dimensionality="xy");

	
	defineConstant("L", 100000);

	
	defineConstant("mu_m1", 1e-10);
	defineConstant("mu_m2", 1e-10);

	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeMutationType("m2", 0.5, "f", 0.0);
	m1.convertToSubstitution = F;
	m2.convertToSubstitution = F;

	initializeGenomicElementType("g1", c(m1, m2), c(mu_m1, mu_m2));
	initializeGenomicElement(g1, 0, L-1);

	initializeMutationRate(mu_m1 + mu_m2);
	initializeRecombinationRate(1e-8);

	
	defineConstant("maxOvules_p1p2", 3);
	defineConstant("maxOvules_p3",   3);
	defineConstant("dd_rate0", 0.4);

	
	defineConstant("hybridizationRate", 0.0001);

	
	defineConstant("sigma_c",    0.20);
	defineConstant("maxDist_c",  0.60);
	defineConstant("gamma_comp", 0.02);

	
	defineConstant("sigma_z", 0.05);

	
	defineConstant("sigma_m",    0.15);
	defineConstant("epsilon_w",  1e-6);
	defineConstant("excludeSelf", T);

	
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=maxDist_c);  // iAll
	i1.setInteractionFunction("n", 1.0, sigma_c);

	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=maxDist_c);  // iP1
	i2.setInteractionFunction("n", 1.0, sigma_c);

	initializeInteractionType(3, "xy", reciprocal=T, maxDistance=maxDist_c);  // iP2
	i3.setInteractionFunction("n", 1.0, sigma_c);

	initializeInteractionType(4, "xy", reciprocal=T, maxDistance=maxDist_c);  // iP3
	i4.setInteractionFunction("n", 1.0, sigma_c);

	
	defineConstant("sb", 0.30);
	defineConstant("hb", 0.50);

	
	defineConstant("nSeed_m1", 5);
	defineConstant("nSeed_m2", 5);

	
	defineConstant("K_p1", 1000);
	defineConstant("K_p2", 1000);
	defineConstant("K_p3", 1000);
	defineConstant("betaK", 2.0);

	defineConstant("OUTFILE", "output_orleach_interPopComp_softK.txt");
}


//////////////////////////////
//  FIRST()

1 first()
{
	sim.addSubpop("p1", 150);
	sim.addSubpop("p2", 150);
	sim.addSubpop("p3", 0);

	
	for (ind in p1.individuals) ind.age = 1;
	for (ind in p2.individuals) ind.age = 1;

	
	seedPrivateDMIs(p1, m1, nSeed_m1);
	seedPrivateDMIs(p2, m2, nSeed_m2);

	
	for (ind in p1.individuals) ind.setValue("z", -0.5);
	for (ind in p2.individuals) ind.setValue("z",  0.5);

	for (ind in p1.individuals) ind.setSpatialPosition(c(ind.getValue("z"), 0.0));
	for (ind in p2.individuals) ind.setSpatialPosition(c(ind.getValue("z"), 0.0));

	writeFile(OUTFILE,
		"gen\tN1\tN2\tN3\n",
		append=F);
}


//////////////////////////////
//  REPRODUCTION

reproduction()
{
	if (individual.age == 0)
		return;

	fertilizationRate = dd_rate0;

	if (subpop == p1) {
		n = asInteger(min(rbinom(1, maxOvules_p1p2, fertilizationRate), 5));
		for (i in seqLen(n)) {
			doHybrid = (runif(1) < hybridizationRate);

			if (doHybrid) {
				candidates = c();
				if (p2.individualCount > 0) candidates = c(candidates, p2.individuals);
				if (p3.individualCount > 0) candidates = c(candidates, p3.individuals);

				if (size(candidates) > 0) {
					mate = chooseMateAssort(individual, candidates);
					kid  = subpop.addCrossed(individual, mate);
					kid.setValue("migrateToP3", T);
					setOffspringTrait(kid, individual, mate);
				} else {
					mate = chooseMateAssort(individual, p1.individuals);
					kid  = subpop.addCrossed(individual, mate);
					setOffspringTrait(kid, individual, mate);
				}
			} else {
				mate = chooseMateAssort(individual, p1.individuals);
				kid  = subpop.addCrossed(individual, mate);
				setOffspringTrait(kid, individual, mate);
			}
		}
		return;
	}

	if (subpop == p2) {
		n = asInteger(min(rbinom(1, maxOvules_p1p2, fertilizationRate), 5));
		for (i in seqLen(n)) {
			doHybrid = (runif(1) < hybridizationRate);

			if (doHybrid) {
				candidates = c();
				if (p1.individualCount > 0) candidates = c(candidates, p1.individuals);
				if (p3.individualCount > 0) candidates = c(candidates, p3.individuals);

				if (size(candidates) > 0) {
					mate = chooseMateAssort(individual, candidates);
					kid  = subpop.addCrossed(individual, mate);
					kid.setValue("migrateToP3", T);
					setOffspringTrait(kid, individual, mate);
				} else {
					mate = chooseMateAssort(individual, p2.individuals);
					kid  = subpop.addCrossed(individual, mate);
					setOffspringTrait(kid, individual, mate);
				}
			} else {
				mate = chooseMateAssort(individual, p2.individuals);
				kid  = subpop.addCrossed(individual, mate);
				setOffspringTrait(kid, individual, mate);
			}
		}
		return;
	}

	if (subpop == p3) {
		n = asInteger(min(rbinom(1, maxOvules_p3, fertilizationRate), 5));
		for (i in seqLen(n)) {
			doBackcross = (runif(1) < hybridizationRate);

			if (doBackcross) {
				candidates = c();
				if (p1.individualCount > 0) candidates = c(candidates, p1.individuals);
				if (p2.individualCount > 0) candidates = c(candidates, p2.individuals);

				if (size(candidates) > 0)
					mate = chooseMateAssort(individual, candidates);
				else
					mate = chooseMateAssort(individual, p3.individuals);
			} else {
				mate = chooseMateAssort(individual, p3.individuals);
			}

			kid = subpop.addCrossed(individual, mate);
			setOffspringTrait(kid, individual, mate);
		}
		return;
	}
}




late()
{
	
	migrateFlaggedHybrids();

	
	for (pop in sim.subpopulations)
		for (ind in pop.individuals)
			ind.fitnessScaling = 1.0;

	
	applyMacArthurCompetitionInterPop();

	
	applyBDMI();

	writeFile(OUTFILE,
		sim.cycle + "\t" +
		p1.individualCount + "\t" +
		p2.individualCount + "\t" +
		p3.individualCount + "\n",
		append=T);
}


2000 late()
{
	sim.simulationFinished();
}
